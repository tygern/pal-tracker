import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    ext {
        springBootVersion = "2.0.0.M7"
    }

    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/milestone/" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

plugins {
    id "java"
    id "org.flywaydb.flyway" version "5.0.5"
}

apply plugin: 'org.springframework.boot'

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/milestone/" }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.2")
    compile("org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-actuator:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-security:$springBootVersion")

    compile("mysql:mysql-connector-java:6.0.6")

    runtime("io.micrometer:micrometer-registry-prometheus:1.0.0-rc.5")

    testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
}

springBoot {
    buildInfo()
}

def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
bootRun.environment([
    "WELCOME_MESSAGE": "hello",
    "SPRING_DATASOURCE_URL": developmentDbUrl,
    "MANAGEMENT_ENDPOINTS_WEB_EXPOSE": "*",
    "MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS": "true",
])

def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
test.environment([
    "WELCOME_MESSAGE": "Hello from test",
    "SPRING_DATASOURCE_URL": testDbUrl,
    "MANAGEMENT_ENDPOINTS_WEB_EXPOSE": "*",
    "MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS": "true",
])

flyway {
    url = developmentDbUrl
    user = "tracker"
    password = ""
    locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
    url = testDbUrl
}
